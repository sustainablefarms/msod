model{

  # community priors for occupancy covariates
  for(m in 1:noccvar) {
    mu.occ.b[m] ~ dnorm(0, 0.01)T(-10,10)
    tau.occ.b[m] <- pow(sigma.occ.b[m], -2)
    sigma.occ.b[m] ~ dunif(0, 10)
  }

  # community priors for detection covariates
  for(m in 1:nobsvar) {
    mu.det.b[m] ~ dnorm(0, 0.01)T(-10,10)
    tau.det.b[m] <- pow(sigma.det.b[m], -2)
    sigma.det.b[m] ~ dunif(0, 10)
  }

  # loop over all species
  for (i in 1:(nspecies)) {


    # random effects for occupancy
    for(m in 1:noccvar) {
      occ.b[i, m] ~ dnorm(mu.occ.b[m], tau.occ.b[m])T(-10,10)
    }

    # random effects for detection
    for(m in 1:nobsvar) {
      det.b[i, m] ~ dnorm(mu.det.b[m], tau.det.b[m])T(-10,10)
    }
    
    # loop over all sites to give occupancy
    for (j in 1:nmodelsites) {

      # probit model for occupancy
      eta[j,i] <-  inprod(occ.b[i, ], Xocc[j, ])
      occ.indicator[j,i] ~ dnorm(eta[j,i],1) 
      occ.v[j,i] <- step(occ.indicator[j,i])
   }
   
   # apply occupancy probabilities to detection
   for (l in 1:nvisits) {
      # logistic model for detection
      logit(p[l,i]) <-   inprod(det.b[i, ], Xobs[l, ])
      mu.p[l,i] <- p[l,i]*occ.v[ModelSite[l],i]
      y[l,i] ~ dbern(mu.p[l,i])
   }
  }

}

